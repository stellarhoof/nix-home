{ config, lib, pkgs, ... }: {
  imports = [
    ../shared/default.nix
    # ./brave.nix
    # ./default-applications.nix
    # ./email.nix
    # ./gtk.nix
    # ./programs/dunst.nix
    # ./programs/mpv.nix
    # ./programs/polybar.nix
    # ./programs/sway.nix
    # ./programs/zathura.nix
    # ./media-keys-scripts.nix
    # ./xorg.nix
  ];

  home.packages = with pkgs; [
    calibre # EBook manager
    dfeet # DBus viewer: https://0pointer.net/blog/the-new-sd-bus-api-of-systemd.html
    font-manager # Font manager
    gparted # Partition manager
    hakuneko # Manga downloader
    libnotify # Send notifications to a desktop notifications daemon
    transmission-qt # BitTorrent downloader
  ];

  # Whether new or changed services that are wanted by active targets
  # should be started. Additionally, stop obsolete services from the
  # previous generation.
  systemd.user.startServices = "sd-switch";

  services.gpg-agent = let ttl = 60480000;
  in {
    defaultCacheTtl = ttl;
    defaultCacheTtlSsh = ttl;
    maxCacheTtl = ttl;
    maxCacheTtlSsh = ttl;
  };

  # Allow fontconfig to discover installed fonts. It runs `fc-cache -f`.
  fonts.fontconfig.enable = true;

  # Yse fonts in personal config as default fonts in fontconfig. Until
  # https://github.com/nix-community/home-manager/pull/2732 gets merged.
  xdg.configFile = let
    mkFontconfigConf = conf: ''
      <?xml version='1.0'?>

      <!-- Generated by Home Manager. -->

      <!DOCTYPE fontconfig SYSTEM 'urn:fontconfig:fonts.dtd'>
      <fontconfig>
      ${conf}
      </fontconfig>
    '';
  in {
    "fontconfig/conf.d/52-hm-default-fonts.conf".text = let
      genDefault = fonts: name:
        lib.optionalString (fonts != [ ]) ''
          <alias binding="same">
            <family>${name}</family>
            <prefer>
            ${
              lib.concatStringsSep "" (map (font: ''
                <family>${font}</family>
              '') fonts)
            }
            </prefer>
          </alias>
        '';
    in mkFontconfigConf ''
      <!-- Default fonts -->
      ${genDefault [ config.mine.fonts.sans.name ] "sans-serif"}
      ${genDefault [ config.mine.fonts.serif.name ] "serif"}
      ${genDefault [ config.mine.fonts.mono.name ] "monospace"}
    '';
  };

  # Whether to enable automatic creation of the XDG user directories.
  # https://wiki.archlinux.org/title/XDG_user_directories
  xdg.userDirs.enable = true;
  xdg.userDirs.createDirectories = true;

  # Configure these directories I don't care about to oblivion.
  xdg.userDirs.desktop = "/tmp/userDirs/Desktop";
  xdg.userDirs.publicShare = "/tmp/userDirs/Public";
  xdg.userDirs.templates = "/tmp/userDirs/Templates";

  # The cursor theme and settings.
  home.pointerCursor.name = "Vanilla-DMZ";
  home.pointerCursor.package = pkgs.vanilla-dmz;
  home.pointerCursor.size = 64;
  home.pointerCursor.gtk.enable = true;
}
